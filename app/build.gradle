plugins {
    id "com.android.application"
    id "com.google.devtools.ksp"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "org.jetbrains.kotlin.android"
    id "androidx.navigation.safeargs.kotlin"
}

android {
    compileSdk 33
    defaultConfig {
        minSdk 26
        targetSdk 33
        versionCode 69
        versionName "2.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments["room.schemaLocation"] = "$projectDir/schemas".toString()
                arguments["room.incremental"] = "true"
                arguments["room.expandProjection"] = "true"
            }
        }
    }
    flavorDimensions "environment"
    productFlavors {
        development {
            applicationId "com.castcle.android.dev.v2"
        }
        production {
            applicationId "com.castcle.android.v2"
        }
        staging {
            applicationId "com.castcle.android.stg.v2"
        }
    }
    buildFeatures {
        viewBinding true
    }
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    applicationVariants.all { variant ->
        variant.sourceSets.java.each {
            it.srcDirs += "build/generated/ksp/${variant.name}/kotlin"
        }
    }
}

dependencies {

    // Android
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.camera:camera-camera2:1.1.0"
    implementation "androidx.camera:camera-core:1.1.0"
    implementation "androidx.camera:camera-lifecycle:1.1.0"
    implementation "androidx.camera:camera-view:1.1.0"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.fragment:fragment-ktx:1.5.3"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"
    implementation "androidx.viewpager2:viewpager2:1.1.0-beta01"
    implementation "androidx.work:work-runtime-ktx:2.7.1"

    // Date Picker
    implementation "com.github.florent37:singledateandtimepicker:2.2.7"

    // Dimension Resource
    implementation "com.intuit.sdp:sdp-android:1.1.0"

    // Facebook
    implementation "com.facebook.android:facebook-android-sdk:14.1.0"

    // Firebase
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-messaging-ktx"
    implementation platform("com.google.firebase:firebase-bom:30.4.0")

    // Glide
    implementation "com.github.bumptech.glide:glide:4.13.2"
    kapt "com.github.bumptech.glide:compiler:4.13.2"

    // Google
    implementation "com.google.android.gms:play-services-auth:20.3.0"
    implementation "com.google.android.gms:play-services-mlkit-barcode-scanning:18.1.0"
    implementation "com.google.android.material:material:1.6.1"

    // Image Picker
    implementation "com.github.Drjacky:ImagePicker:2.3.18"

    // Image Viewer
    implementation "com.github.stfalcon-studio:StfalconImageViewer:v1.0.1"

    // Jwt Decode
    implementation "com.auth0.android:jwtdecode:2.0.1"

    // Koin
    implementation "io.insert-koin:koin-android:3.2.0"
    implementation "io.insert-koin:koin-annotations:1.0.1"
    implementation "io.insert-koin:koin-androidx-workmanager:3.2.0"
    ksp "io.insert-koin:koin-ksp-compiler:1.0.1"

    // Lottie
    implementation "com.airbnb.android:lottie:3.4.1"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.2"

    // Network
    implementation "com.squareup.okhttp3:logging-interceptor"
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.3")

    // Permission
    implementation "com.guolindev.permissionx:permissionx:1.6.4"

    // Reactivex
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    // Room
    implementation "androidx.room:room-ktx:2.4.3"
    implementation "androidx.room:room-paging:2.4.3"
    implementation "androidx.room:room-rxjava2:2.4.3"
    kapt "androidx.room:room-compiler:2.4.3"

    // Shimmer Effect
    implementation "com.facebook.shimmer:shimmer:0.5.0"

    // Timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Time Ago
    implementation "com.github.perfomer:blitz:1.0.10"

    // Twitter
    implementation("com.twitter.sdk.android:twitter:3.3.0") {
        exclude group: "com.android.support"
    }

}