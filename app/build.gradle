import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "com.android.application"
    id "com.google.devtools.ksp"
    id "com.google.firebase.crashlytics"
    id "com.google.gms.google-services"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "org.jetbrains.kotlin.android"
    id "androidx.navigation.safeargs.kotlin"
}

android {
    namespace "com.castcle.android"
    compileSdk 34
    defaultConfig {
        minSdk 26
        targetSdk 34
        versionCode 169
        versionName "2.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments["room.schemaLocation"] = "$projectDir/schemas".toString()
                arguments["room.incremental"] = "true"
                arguments["room.expandProjection"] = "true"
            }
        }
    }
    flavorDimensions += "environment"
    productFlavors {
        development {
            applicationId "com.castcle.android.dev"
        }
        production {
            applicationId "com.castcle.android"
        }
        staging {
            applicationId "com.castcle.android.stg"
        }
    }
    buildFeatures {
        viewBinding true
    }
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.configureEach { variant ->
        kotlin.sourceSets {
            named(name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
    tasks.withType(KotlinCompile).configureEach {
        it.kotlinOptions.jvmTarget = "1.8"
    }
}

dependencies {

    // Android
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.camera:camera-camera2:1.2.3"
    implementation "androidx.camera:camera-core:1.2.3"
    implementation "androidx.camera:camera-lifecycle:1.2.3"
    implementation "androidx.camera:camera-view:1.2.3"
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.fragment:fragment-ktx:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.paging:paging-runtime-ktx:3.2.0"
    implementation "androidx.viewpager2:viewpager2:1.1.0-beta02"
    implementation "androidx.work:work-runtime-ktx:2.8.1"

    // Date Picker
    implementation "com.github.florent37:singledateandtimepicker:2.2.7"

    // Dimension Resource
    implementation "com.intuit.sdp:sdp-android:1.1.0"

    // Facebook
    implementation "com.facebook.android:facebook-android-sdk:16.0.1"

    // Firebase
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-config-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-messaging-ktx"
    implementation platform("com.google.firebase:firebase-bom:32.2.2")

    // Glide
    implementation "com.github.bumptech.glide:glide:4.15.1"
    ksp "com.github.bumptech.glide:ksp:4.15.1"

    // Google
    implementation "com.google.android.gms:play-services-auth:20.6.0"
    implementation "com.google.android.gms:play-services-mlkit-barcode-scanning:18.3.0"
    implementation "com.google.android.material:material:1.9.0"

    // Image Picker
    implementation "com.github.Drjacky:ImagePicker:2.3.18"

    // Image Viewer
    implementation "com.github.stfalcon-studio:StfalconImageViewer:v1.0.1"

    // Jwt Decode
    implementation "com.auth0.android:jwtdecode:2.0.2"

    // Koin
    implementation "io.insert-koin:koin-android:3.4.3"
    implementation "io.insert-koin:koin-androidx-workmanager:3.4.3"
    implementation "io.insert-koin:koin-annotations:1.2.2"
    ksp "io.insert-koin:koin-ksp-compiler:1.2.2"

    // Lottie
    implementation "com.airbnb.android:lottie:3.4.1"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.0"

    // Network
    implementation "com.squareup.okhttp3:logging-interceptor"
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation platform("com.squareup.okhttp3:okhttp-bom:4.9.3")

    // Permission
    implementation "com.guolindev.permissionx:permissionx:1.6.4"

    // Reactivex
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    // Room
    implementation "androidx.room:room-ktx:2.5.2"
    implementation "androidx.room:room-paging:2.5.2"
    implementation "androidx.room:room-rxjava2:2.5.2"
    ksp "androidx.room:room-compiler:2.5.2"

    // Shimmer Effect
    implementation "com.facebook.shimmer:shimmer:0.5.0"

    // Timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Time Ago
    implementation "com.github.perfomer:blitz:1.0.10"

    // Twitter
    implementation("com.twitter.sdk.android:twitter:3.3.0") {
        exclude group: "com.android.support"
    }

}